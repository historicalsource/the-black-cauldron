[logics for room 22 

%include "rm22.msg"
%include "gamedefs"


%define	gurgiland.loaded		lf0
%define	pig.room.loaded		lf1
%define	gwyth.room.loaded		lf2


if (init.log)
	{
	set.horizon( 61);

 	if ((pig.in.castle || pig.safe))
		{
		random( 0, 255, work);
		if (work < chance.gurgi.shows)
			{
			load.logics( gurgiland);	[	room 104
			set( gurgiland.loaded);
			}
		}

	if (t.with.pig)
		{
		load.logics( pig.room);		[	room 116
		set( pig.room.loaded);
		set( can.lose.pig);

		random( 0, 255, work);
 		if (work < gwyth.showing)
			{
			load.logics( gwyth.room);[	room 114
			set( gwyth.room.loaded);
			}
		}

	load.pic( current.room);
	draw.pic( current.room);
	discard.pic( current.room);


	draw( ego);

	if (gurgiland.loaded)
		{
		call( gurgiland);
		position( a.gurgi, 0, 155);
		}

	if (pig.room.loaded)
		{
		call( pig.room);
		}

	if (gwyth.room.loaded)
		{
		call( gwyth.room);
		}

	show.pic();
	return();

	}


if (lookit)
	{
	if (!gurgi.here)
		{
		print( 1);
		}
	}


if (!doit && !useit)		{goto no.input;}
if (useit)				{goto use.it;}


[*****
:do.it
:use.it
:no.input
:exit
[*****

if (gwyth.room.loaded)
	{
	call( gwyth.room);
	}

if (pig.room.loaded)
	{
	call( pig.room);
	}

if (gurgiland.loaded)
	{
	call( gurgiland);
	}

if (edge.ego.hit == top)			{new.room( 17);}
if (edge.ego.hit == bottom)		{new.room( 2);}
if (edge.ego.hit == right)		{new.room( 23);}
if (edge.ego.hit == left)		{new.room( 21);}
