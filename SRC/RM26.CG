[logics for room 26 -- Canyon Climber west

%include  "rm26.msg"
%include "gamedefs"


%define	horizon				40

%define	end.of.rope			lf1
%define	change.positions		lf2
%define	read.message			lf3
%define	hit.bottom			lf4
%define	read.message.1			lf5
%define	read.message.2			lf6
%define	falling.sign			lf7

%define	start.walking			lv0


if (init.log)
	{
	reset( ego.falling);
	room.bottom = screen.bottom;
	ego.climbing = walking;

	load.sound( s.ego.falling);
	load.view( v.ego.climbing.rope.west);
	load.view( v.ego.climbing.wall);
	load.view( v.ego.falling);
	load.view( v.rope.west);

	set.horizon( horizon);

	load.pic( current.room);
	draw.pic( current.room);
	discard.pic( current.room);

	if (rope.west)
		{
		add.to.picture( v.rope.west, 0, 0, 141, 118, 4, 4);
		}

	set.view( ego, v.ego);
	set.priority( ego, 14);
	start.motion( ego);

	if (previous.room == 29)			[	position ego
		{
		position( ego, 150, 53);
		move.obj( ego, 148, 53, 0, done);
		}
	else
		{
		position( ego, 140, 142);
		}

	draw( ego);

	show.pic();

	if (debugging)
		{
		print( 12);
		}

	if (previous.room == 25)
		{
		print( 4);
		}

	return();

	}


if (ego.falling)
	{
	if ((ego.y > 120 || 			[	so he's below the bridge
		ego.x > 120))				[	to skip the add.to.pic
		{
		set.priority( ego, 15);
		}
	goto exit;
	}


if (lookit)						[	pressed "look" button
	{
	set( have.match);
	if (ego.climbing == walking)
		{
		print( 4);				[	"You're on a ledge
		}
	else
		{
		if (ego.climbing == on.rope.west)
			{
			print( 5);			[	"You're on a rope
			}
		else
			{
			print( 6);			[	"Your finger's in a crack
			}
		}
	}

if (!doit && !useit)
	{
	goto no.input;
	}

if (useit)
	{
	goto use.it;
	}


[*****
:do.it							[	pressed "do something"
[*****

if (rope.west)
	{
	if (posn( ego, 135, 131, 159, 159))
		{
		set( have.match);
		if (ego.climbing == walking)
			{
			ego.climbing = on.rope.west;
			print( 1);			[	"you grab the rope and descend
			set.view( ego, v.ego.climbing.rope.west);
			start.motion( ego);
			erase( ego);
			ignore.blocks( ego);
			set( ignore.special);
			position( ego, 140, 133);
			draw( ego);
			move.obj( ego, 141, 134, 0, end.of.rope);
			}
		else
			{
			ego.climbing = walking;
			set.view( ego, v.ego);
			start.motion( ego);
			work = 1;
			step.size( ego, work);
			erase( ego);
			observe.blocks( ego);
			reset( ignore.special);
			draw( ego);
			position( ego, 140, 142);
			}
		}
	}

goto no.input;


[*****
:use.it							[	pressed "use current obj"
[*****


if (object.used == u.dagger)
	{
	print( 3);					[	"you dull your dagger on a rock
	set( have.match);
	}


[*****
:no.input
[*****

if  (posn( ego, 133, 131, screen.right, screen.bottom) &&[	near rope
	previous.room == 29 &&
    !read.message)
	{
	set( read.message);
	print( 7);					[	"Made it.
	}

if  (posn( ego, 140, screen.top, screen.right, 47) &&	[	at top of cliff
	previous.room == 25 &&
	!read.message)
	{
	set( read.message);
	print( 7);					[	"Made it.
	}


if (posn( ego, 142, 0, 159, 52))		[	upper right corner
	{
	position( ego, 150, 50);
	set.view( ego, v.ego);
	}

[	if (ego.climbing != walking &&
[		posn( ego, 128, 52, 159, 58))
[		{
[		start.walking = 6;
[		ego.x = 135;
[		ego.y = 52;
[		}

if (on.water)
	{
	if (posn( ego, 88, 126, 111, 128))
		{
		if (ego.climbing == walking)
			{
			start.climbing = 1;
			ego.x = 96;
			ego.y = 125;
			}
		else
			{
			start.walking = 1;
			ego.x = 99;
			ego.y = 129;
			}
		}

	if (posn( ego, 74, 147, 90, 149))
		{
		if (ego.climbing == walking)
			{
			start.climbing = 2;
			ego.x = 74;
			ego.y = 146;
			}
		else
			{
			start.walking = 2;
			ego.x = 76;
			ego.y = 150;
			}
		}

	if (posn( ego, 38, 144, 60, 146))
		{
		if (ego.climbing == walking)
			{
			start.climbing = 3;
			ego.x = 46;
			ego.y = 143;
			}
		else
			{
			start.walking = 3;
			ego.x = 49;
			ego.y = 147;
			}
		}

	if (posn( ego, 50, 73, 64, 75))
		{
		if (ego.climbing == walking)
			{
			start.climbing = 4;
			ego.x = 56;
			ego.y = 72;
			}
		else
			{
			start.walking = 4;
			ego.x = 59;
			ego.y = 76;
			}
		}

	if (posn( ego, 113, 72, 130, 74))
		{
		if (ego.climbing == walking)
			{
			start.climbing = 5;
			ego.x = 117;
			ego.y = 71;
			}
		else
			{
			start.walking = 5;
			ego.x = 120;
			ego.y = 75;
			}
		}

	if (posn( ego, 127, 49, 145, 51))
		{
		if (ego.climbing == walking)
			{
			start.climbing = 6;
			ego.x = 132;
			ego.y = 48;
			}
		else
			{
			start.walking = 6;
			ego.x = 135;
			ego.y = 52;
			}
		}
	}


if (start.climbing)
	{
	start.climbing = 0;
	ego.climbing = on.wall;
	set.priority( ego, 12);
	start.motion( ego);
	observe.blocks( ego);
	set.view( ego, v.ego.climbing.wall)
	reposition.to.v( ego, ego.x, ego.y);
	if (!read.message.1)
		{
		set( read.message.1);
		print( 8);			["you stick your fingers in your crack
		}
	}

if (start.walking)
	{
	start.walking = 0;
	ego.climbing = walking;
	set.priority( ego, 14);
	step.size( ego, one);
	start.motion( ego);
	observe.blocks( ego);
	set.view( ego, v.ego);
	reposition.to.v( ego, ego.x, ego.y);
	if (!read.message.2)
		{
		set( read.message.2);
		print( 9);			[	"You drop to the ledge
		}
	}


if ((posn( ego, 86, 81, 99, 86) ||		[	on the natural bridge
	posn( ego, 54, 76, 58, 86) ||
	posn( ego, 80, 80, 83, 89) ||
	posn( ego, 70, 80, 79, 88)))
	{
	if (ego.climbing == walking)
		{
		reset( ignore.special);
		ignore.blocks( ego);
		}
	else
		{
		set( ignore.special);
		observe.blocks( ego);
		}
	}
else
	{
	if (!ego.falling)
		{
		reset( ignore.special);
		observe.blocks( ego);
		}
	}

if (ego.climbing == on.wall)			[	ignore the special that makes
	{							[	him fall if he's walking
	if ((posn( ego,  74, 124,  80, 147)	|| 
		posn( ego,  78,  79,  98,  89)	||
		posn( ego,  34,  72,  53,  86)	||
		posn( ego, 112,  45, 131,  57)))
		{
		set( ignore.special);
		}
	else
		{
		reset( ignore.special);
		}
	}


if ((posn( ego, 85, 128, 93, 148)	||
	posn( ego, 48, 72, 58, 87)))
	{
	if (ego.climbing == walking)		[	ignore the block that keeps him
		{						[	from the hump, if walking
		ignore.blocks( ego);
		}
	else
		{
		observe.blocks( ego);
		}
	}


if (posn( ego, 135, 131, screen.right, screen.bottom))	[	near the rope
	{
	if (ego.climbing == on.rope.west)
		{
		set( ignore.special);
		ignore.blocks( ego);
		}
	else
		{
		reset( ignore.special);
		observe.blocks( ego);
		}
	}

if  (hit.special && 				[	handle klutzes
    !ignore.special && 
    !ego.falling)
	{
	if (force.a.test &&
		monitor.type != mono)
		{
		toggle( falling.sign);
		if (falling.sign)
			{
			display( 22, 30, 10);
			}
		else
			{
			display( 22, 31, 10);
			}
		}
	else
		{
		set( ego.falling);
		set.view( ego, v.ego.falling);
		sound( s.ego.falling, done);
		set( keep.cycling);
		ignore.blocks( ego);
		if (posn( ego, 79, 77, 85, 79))[	falling off left end of bridge
			{
			x = 6;
			y = 1;
			reposition( ego, x, y);
			set.priority( ego, 11);
			}
		else
			{
			if (posn( ego, 86, 79, 100, 80)) [	falling off the middle of bridge
				{
				set.priority( ego, 11);
				}
			else
				{
				if (posn( ego, 101, 74, 108, 80))	[	right end of bridge
					{
					x = -6;
					y = 1;
					reposition( ego, x, y);
					set.priority( ego, 11);
					}
				else
					{
					if (posn( ego, 79, 56, 98, 73)) [	climbing behind bridge
						{
						set.priority( ego, 11);
						}
					else
						{
						set.priority( ego, 15);	[	everywhere else is ok!
						}
					}
				}
			}
		get.posn( ego, x, y);
		step = 4;
		move.obj.f( ego, x, room.bottom, step, hit.bottom);
		}
	}
else
	{
	if (force.a.test &&
		monitor.type != mono)
		{
		display( 22, 30, 11);
		}
	}


if (!cliff.first.time &&
	posn( ego, 139, 50, 155, 59))
	{
	score += 6;
	set( cliff.first.time);
	}



[*****
:exit							[test for leaving the room
[*****

if (hit.bottom)
	{
	if (debugging)
		{
		reset( ego.falling);
		reset( keep.cycling);
		current.ego.view = normal;
		current.room = 25;
		new.room( 26);
		}
	else
		{
		erase( ego);
		certain.death = 1;
		}
	}

if (end.of.rope)
	{
	reset( ignore.special);
	new.room( 25);
	}

if (posn( ego, 142, 0, 159, 52))
	{
	set.view( ego, v.ego);
	start.motion( ego);
	new.room( 29);
	}

if (edge.ego.hit == right && !ego.falling)
	{
	start.motion( ego);
	get.posn( ego, x, y);
	if (y < 80)
		{
		set.view( ego, v.ego);
		new.room( 29);
		}
	else
		{
		new.room( 25);
		}
	}
