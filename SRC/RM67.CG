[logics for room 67 -- rm.titles

%include "rm67.msg"
%include "gamedefs"


%define	mono.copyright.delay	 40
%define	screen.1.delay			115
%define	screen.2.delay			111
%define	box.screen			68
							
%define	start.the.game			lf0
							
%define	screen.number			lv0
%define	music.start			lv1


if (init.log)
	{
	load.sound( m.theme.song);
	work = box.screen;
	load.pic( work);
	wait.1 = screen.1.delay;

	prevent.input( );
		
	load.pic( current.room);
	draw.pic( current.room);
	discard.pic( current.room);

	if (monitor.type != mono)
		{
		display( 22, 2, 1);				[	row, col, message
		display( 23, 3, 2);
		display( 24, 1, 3);
		}

	screen.number = 1;
	music.start = 2;				[	to prevent 1st note, if keypress

	show.pic();
	return();

	}


if ((have.key( ) || doit || useit || lookit))
	{
	set( start.the.game);
	}

--wait.1;
if  (wait.1 == mono.copyright.delay &&
	screen.number == 1 &&
	monitor.type == mono)
	{
	set( leave.window);				[	What a piece of shit this is!
	print.at( 4, 2, 2, 37);
	}

if (wait.1 == 1 && screen.number == 1)
	{
	work = box.screen;
	draw.pic( work);
	show.pic( );
	if (monitor.type == mono)
		{
		close.window();
		}
	else
		{
		clear.lines( 22, 24, bg.color);
		}

	set.text.attribute( 12, bg.color);

		    [y (>2 & <19), x (> 2), message#
	display(  3,  4,  5);
	display(  4,  9,  6);
	display(  5,  9,  7);
	display(  6,  9,  8);
	display(  7,  9,  9);
	display(  8,  9, 10);

	display( 10,  4, 11);
	display( 11,  9, 12);
	display( 12,  9, 13);
	display( 13,  9, 14);
	display( 14,  9, 15);
	display( 15,  9, 16);
	display( 16,  9, 17);
			   
	display( 18,  4, 18);
	display( 19,  9, 19);

	set.text.attribute( text.color, bg.color);
	++screen.number;
	wait.1 = screen.2.delay;
	}

if (wait.1 == 1 && screen.number == 2)
	{
	set( start.the.game);
	}

--music.start;
if (music.start == 1)
	{
	sound( m.theme.song, start.the.game);
	}


if (start.the.game)
	{
	if (monitor.type != mono)
		{
		clear.lines( 22, 24, bg.color);
		}
	set( enable.menu);
	disable.item( c.dummy);
	new.room( rm.restart);
	}
