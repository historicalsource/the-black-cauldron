[logics for room 57 -- left corner of lower hallway

%include "rm57.msg"
%include "gamedefs"


%define	eilonwy.room.loaded		lf0
%define	pig.room.loaded		lf1

%object	a.torch1				small1
%object	a.torch2				small2



if (init.log)
	{
	set.horizon( 98);

	set( t.in.basement);
	if (e.in.basement)
		{
		load.logics( eilonwy.room);
		set( eilonwy.room.loaded);
		}

	if (pig.in.basement)
		{
		load.logics( pig.room);
		set( pig.room.loaded);
		}
	
	load.view( v.torch);

	load.pic( current.room);
	draw.pic( current.room);
	discard.pic( current.room);

	animate.obj( a.torch1);
	set.view( a.torch1, v.torch);
	position( a.torch1, 21, 77);
	ignore.horizon( a.torch1);

	animate.obj( a.torch2);
	set.view( a.torch2, v.torch);
	position( a.torch2, 151, 86);
	ignore.horizon( a.torch2);


	if (previous.room == 54)
		{
		position( ego, 30, 99);
		}
	else
		{
		if (previous.room == 58)
			{
			get.posn( ego, x, y);
			if (y < 121)
				{
				y = 121;
				position.f( ego, x, y);
				}
			}
		}

	draw( a.torch1);
	draw( a.torch2);

	draw( ego);

	if (eilonwy.room.loaded)
		{
		call( eilonwy.room);
		}

	if (pig.room.loaded)
		{
		call( pig.room);
		}

	show.pic();
	return();

	}



if (lookit)
	{
	print( 1);
	}


[*****
:exit
[*****

if (edge.ego.hit == top)			{new.room( 54);}
if (edge.ego.hit == right)		{new.room( 58);}

if (pig.room.loaded)
	{
	call( pig.room);
	}

if (eilonwy.room.loaded)
	{
	call( eilonwy.room);
	}
