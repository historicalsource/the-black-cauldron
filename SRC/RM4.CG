[logics for room 4 -- Caer Dallben east

%include "rm4.msg"
%include "gamedefs"


%define	gwyth.room.loaded		lf0
%define	pig.room.loaded		lf1
%define	gurgiland.loaded		lf2


if (init.log)
	{

	set.horizon( 47);

 	if ((pig.in.castle || pig.safe))
		{
		random( 0, 255, work);
		if (work < chance.gurgi.shows)
			{
			load.logics( gurgiland);	[	room 104
			set( gurgiland.loaded);
			}
		}

	if (t.with.pig)
		{
		load.logics( pig.room);		[	room 116
		set( pig.room.loaded);
		set( can.lose.pig);

		random( 0, 255, work);
 		if (work < gwyth.showing)
			{
			load.logics( gwyth.room);	[	room 114
			set( gwyth.room.loaded);
			}
		}


	load.pic( current.room);
	draw.pic( current.room);
	discard.pic( current.room);


	if (previous.room == 24)
		{
		position( ego, 64, 51);
		}

	draw( ego);
	if (gurgiland.loaded)
		{
		call( gurgiland);
		position( a.gurgi, 0, 98);
		}

	if (pig.room.loaded)
		{
		call( pig.room);
		}

	if (gwyth.room.loaded)
		{
		call( gwyth.room);
		}

	show.pic();
	return();

	}


if (lookit && !gurgi.here)
	{
	if (posn( ego, 0, 0, 110, 167))
		{
		print( 1);
		}
	else
		{
		print( 2);
		}
	}

 
[*****
:do.it
:use.it
:no.input
:exit
[*****

if (gwyth.room.loaded)
	{
	call( gwyth.room);
	}

if (pig.room.loaded)
	{
	call( pig.room);
	}

if (gurgiland.loaded)
	{
	call( gurgiland);
	}

if (edge.ego.hit == top)			{new.room( 24);}
if (edge.ego.hit == bottom)		{new.room( 9);}
if (edge.ego.hit == left)		{new.room( 3);}
	
